---
page_title: "shoreline_file Resource - terraform-provider-shoreline"
subcategory: ""
description: |-
  Automatically distribute artifacts across your Shoreline Resources.
---

# shoreline_file (Resource)

The [File](https://docs.shoreline.io/op/commands/cp) allows you to easily transmit critical files throughout your fleet, even to ephemeral [Resources](https://docs.shoreline.io/platform/resources) such as Kubernetes (k8s) containers. This technique is potent when you need to distribute and execute custom bash scripts or other critical files without the need for manual intervention.

## Required Properties

A [File](https://docs.shoreline.io/op/commands/cp) must know where to copy from and where to distribute to.  The required properties are:

- name - A unique object name.
- destination_path - The relative, destination file path.
- input_file - The relative, local file path of the source artifact.
- [resource_query](https://docs.shoreline.io/platform/resources) - The target Shoreline [Resources](https://docs.shoreline.io/platform/resources) to distribute the artifact to.

## Usage

The following example distributes the local `<terraform_module_directory>/data/jvm_dumps.sh` file the target Shoreline [Resources](https://docs.shoreline.io/platform/resources) defined by the `resource_query` Terraform variable:

```terraform
# Push the script that actually performs the JVM stack dump to the selected nodes.
resource "shoreline_file" "jvm_trace_dump_script" {
  name             = "${var.namespace}_dump_script"
  description      = "Script to dump JVM stack traces."
  input_file       = "${path.module}/data/jvm_dumps.sh" # source file (relative to this module)
  destination_path = "${var.script_path}/jvm_dumps.sh"  # where it is copied to on the selected resources
  resource_query   = var.resource_query                 # which resources to copy to
  enabled          = true
}
```

```terraform
variable "namespace" {
  type        = string
  description = "A namespace to isolate multiple instances of the module with different parameters."
}

variable "resource_query" {
  type        = string
  description = "The set of hosts/pods/containers monitored and affected by this module."
}

variable "jvm_process_regex" {
  type        = string
  description = "A regular expression to match and select the monitored Java processes."
}

variable "mem_threshold" {
  type        = number
  description = "The high-water-mark, in Mb, above which the JVM process stack-trace is dumped."
  default     = 2000
}

variable "check_interval" {
  type        = number
  description = "Frequency, in seconds, to check the memory usage."
  default     = 60
}

variable "script_path" {
  type        = string
  description = "Destination (on selected resources) for the check, and stack-dump scripts."
  default     = "/agent/scripts"
}

variable "s3_bucket" {
  type        = string
  description = "Destination in AWS S3 for stack-dump output files."
  default     = "shore-oppack-test"
}
```

-> See the Shoreline [Op: `cp` documentation](https://docs.shoreline.io/op/commands/cp) for more info.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- **destination_path** (String) Target location for a copied distributed File object.  See [Op: cp](https://docs.shoreline.io/op/commands/cp).
- **input_file** (String) The local source of a distributed File object.
- **name** (String) The name of the object (must be unique).
- **resource_query** (String) A set of Resources (e.g. host, pod, container), optionally filtered on tags or dynamic conditions.

### Optional

- **description** (String) A user-friendly explanation of an object.
- **enabled** (Boolean) If the object is currently enabled or disabled. Defaults to `false`.
- **id** (String) The ID of this resource.

### Read-Only

- **checksum** (String) Cryptographic hash (e.g. md5) of a File Resource.
- **file_data** (String) Internal representation of a distributed File object's data (computed).
- **file_length** (Number) Length, in bytes, of a distributed File object (computed)
- **type** (String) The type of object (i.e., Alarm, Action, Bot, Metric, Resource, or File).